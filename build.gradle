/*
 * Copyright 2020 Aiven Oy
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

allprojects {
    // https://docs.gradle.org/current/userguide/java_library_plugin.html
    apply plugin: 'java'

    // https://docs.gradle.org/current/userguide/distribution_plugin.html
    apply plugin: 'distribution'

    // https://docs.gradle.org/current/userguide/checkstyle_plugin.html
    apply plugin: 'checkstyle'

    // https://docs.gradle.org/current/userguide/publishing_maven.html
    apply plugin: "maven-publish"

    apply plugin: "idea"

    ext {
        slf4jVersion = "1.7.30"
        openSearchVersion = "1.0.0-SNAPSHOT"
        openSearchClusterRunnerVersion = "7.10.2.0"
        // since jackson-core is in the main classpath
        // plz keep the same version as for OpenSearch
        jacksonDatabind = "2.10.4"
    }


    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    repositories {
        mavenCentral()
        maven {
            url "https://aws.oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    distributions {

        main {
            contents {
                from jar
                from configurations.runtimeClasspath

                from(sourceSets.main.output.resourcesDir) {
                    include "*.properties", "*.policy"
                }
                from(projectDir) {
                    include "README*", "LICENSE*", "NOTICE*"
                }

                into '/'
            }
        }
    }

    processResources {
        filesMatching('plugin-descriptor.properties') {
            expand(version: project.version,
                    javaVersion: java.sourceCompatibility,
                    openSearchVersion: openSearchVersion
            )
        }
    }


    sourceSets {
        integrationTest {
            java.srcDir file('src/integration-test/java')
            resources.srcDir file('src/integration-test/resources')
            compileClasspath += sourceSets.main.output + configurations.testRuntime
            runtimeClasspath += output + compileClasspath
        }
    }

    idea {
        module {
            testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
            testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
        }
    }

    checkstyle {
        toolVersion "8.36"
        configDir rootProject.file("checkstyle/")
    }

    test {
        useJUnitPlatform()
        minHeapSize = "1024m"
        maxHeapSize = "1024m"
    }

    configurations {
        integrationTestImplementation.extendsFrom testImplementation
        integrationTestRuntime.extendsFrom testRuntime
    }

    dependencies {
        implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonDatabind") {
            exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        }

        compileOnly "org.opensearch:opensearch:$openSearchVersion"

        implementation("org.apache.logging.log4j:log4j-slf4j-impl:2.11.1") {
            exclude group: "org.slf4j", module: "slf4j-api"
            exclude group: "org.apache.logging.log4j", module: "log4j-api"
            exclude group: "org.apache.logging.log4j", module: "log4j-core"
        }

        compile "org.slf4j:slf4j-api:$slf4jVersion"

        testImplementation "org.mockito:mockito-core:3.5.7"
        testImplementation "org.mockito:mockito-junit-jupiter:3.5.7"
        // XXX fix: testImplementation "org.codelibs:opensearch-cluster-runner:$openSearchClusterRunnerVersion"
	// earlier this came from the ECR dependency?
        testImplementation "org.opensearch:opensearch:$openSearchVersion"
        testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'

        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

        // Make test utils from 'test' available in 'integration-test'
        // XXX fix: integrationTestImplementation "org.codelibs:opensearch-cluster-runner:$openSearchClusterRunnerVersion"
	// earlier this came from the ECR dependency?
        integrationTestImplementation "org.opensearch:opensearch:$openSearchVersion"

        integrationTestImplementation sourceSets.test.output
    }

}
